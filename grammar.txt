<program> := program ( ) { <statement-list> }
<statement-list> := <statement> <statement-list>
<statement-list> := <statement>
<statement> := <declaration> ;
<statement> := <assignment> ;
<statement> := <row>
<declaration> := declare list of variables <identifier-list> : <type>
<declaration> := declare <identifier> : <type>
<identifier-list> := <identifier>
<identifier-list> := <identifier> <identifier-list>
<type> := <primitive_type>
<type> := <array>
<primitive_type> := integer
<primitive_type> := real
<primitive_type> := boolean
<array> := <rectangular_array>
<array> := <jagged_array>
<rectangular_array> := array <enumeration-list> of integer
<enumeration-list> := <enumeration>
<enumeration-list> := <enumeration> <enumeration-list>
<enumeration> := <static_enumeration>
<enumeration> := <dynamic_enumeration>
<static_enumeration> := [ <constant> .. <constant> ]
<dynamic_enumeration> := [ <identifier> .. <identifier> ]
<dynamic_enumeration> := [ <constant> .. <identifier> ]
<dynamic_enumeration> := [ <identifier> .. <constant> ]
<jagged_array> := jagged array <static_enumeration> <brackets> of integer
<brackets> := [ ]
<brackets> := [ ] [ ]
<row> := R1 [ <constant> ] : size <constant> : values { <constant-list> }
<constants> := <constant>
<constant-list> := <constants>
<constant-list> := <constants> ; <constant-list>
<constants> := <constant> <constants>
<assignment> := <assignee> = <expression>
<assignee> := <identifier>
<assignee> := <identifier> [ <dimensions> ]
<expression> := <boolean_expression>
<expression> := <arithmetic_expression>
<term> := <boolean_term>
<term> := <arithmetic_term>
<boolean_term> := <identifier>
<boolean_expression> := <boolean_term>
<boolean_expression> := <boolean_term> ||| <boolean_expression>
<boolean_term> := <identifier> &&& <boolean_term>
<arithmetic_expression> := <arithmetic_term>
<arithmetic_expression> := <arithmetic_term> <addsub> <arithmetic_expression>
<addsub> := +
<addsub> := -
<arithmetic_term> := <constant>
<arithmetic_term> := <identifier>
<arithmetic_term> := <array_term>
<array_term> := <identifier> [ <dimensions> ]
<dimensions> := <constant>
<dimensions> := <constant> <dimensions>
<arithmetic_term> := <constant> <muldiv> <arithmetic_term>
<arithmetic_term> := <identifier> <muldiv> <arithmetic_term>
<arithmetic_term> := <array_term> <muldiv> <arithmetic_term>
<muldiv> := *
<muldiv> := /
